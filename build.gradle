import jodd.http.HttpRequest

import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.ratpack:ratpack-gradle:0.9.19"
        classpath "org.jodd:jodd-http:3.6.6"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.2"
}

apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: "idea"
apply plugin: 'maven-publish'

apply from: "gradle/idea/idea.gradle"
apply from: "gradle/dependencyRules.gradle"
apply from: "gradle/browserTest.gradle"
apply from: "gradle/heroku.gradle"
apply from: "gradle/codenarc/codenarc.gradle"

mainClassName = "ratpack.rest.Main"

repositories {
    mavenCentral()
}

ext {
    versions = [
        logback: "1.1.3",
        jodd:    "3.6.6",
        spock:   "1.0-groovy-2.4"
    ]

    bintrayApiKey  = '<no-key>'
    bintrayUser    = '<no-user>'

    releaseVersion = calculateVersion('0.1-SNAPSHOT')
}

version = '0.1-SNAPSHOT'

dependencies {
    compile ratpack.dependency("session")
    compile ratpack.dependency("jackson-guice")

    compile "com.stormpath.sdk:stormpath-sdk-httpclient:1.0.RC4.5"
    compile "ch.qos.logback:logback-classic:${versions.logback}"
    compile "org.hibernate:hibernate-validator:5.2.1.Final"
    compile "javax.el:javax.el-api:2.2.4"
    compile 'org.neo4j:neo4j:2.2.3'

    testCompile("org.spockframework:spock-core:${versions.spock}")
    testCompile("org.jodd:jodd-http:${versions.jodd}")
    testCompile 'org.codehaus.gpars:gpars:1.2.1'

    browserTestCompile "org.gebish:geb-spock:0.12.0"
    browserTestCompile "org.seleniumhq.selenium:selenium-firefox-driver:2.46.0"
    browserTestCompile("org.spockframework:spock-guice:${versions.spock}")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId    'com.energizedwork'
            artifactId 'ratpack-rest'
            version    releaseVersion

            from       components.java
        }
    }
}

bintray {
    dryRun = false
    publish = true

    key = bintrayApiKey
    user = bintrayUser

    pkg {
        repo = 'maven'
        name = 'ratpack-rest'
        desc = 'Ratpack module for quickly creating REST endpoints.'

        licenses = ['Apache-2.0']

        websiteUrl = 'https://github.com/guspower/ratpack-rest'
        issueTrackerUrl = 'https://github.com/guspower/ratpack-rest/issues'
        vcsUrl = 'https://github.com/guspower/ratpack-rest.git'

        labels = ['ratpack', 'rest', 'groovy']
        publicDownloadNumbers = true

        version {
            name = releaseVersion

            mavenCentralSync {
                sync = false
            }
        }
    }

}

test.doFirst {
    test.jvmArgs += ["-Dtest.report.dir=${testReportDir.absolutePath}"]
}

String calculateVersion(String version) {
    String result = version
    if(version.endsWith('SNAPSHOT')) {
        def format = new SimpleDateFormat('yyyyMMddHHmmss')
        format.setCalendar(Calendar.getInstance(TimeZone.getTimeZone('UTC')));
        String date = format.format(new Date())
        result = version.replace('SNAPSHOT', date)
    }
    result
}

task travis << {
    def xml = HttpRequest.get('https://api.travis-ci.org/repos/guspower/ratpack-rest/cc.xml').send().body()
    def data = new XmlSlurper().parseText(xml)
    String status = data.Project.'@lastBuildStatus'.text()
    if(!status.toLowerCase().contains('success')) {
        throw new Exception("Travis build failure - [$status]")
    }
}
